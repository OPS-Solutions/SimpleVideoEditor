@ECHO off
ECHO This batch script was generated by Simple Video Editor
ECHO #Batch inputs
ECHO WorkingDir= %cd%
ECHO BatchDir= %~dp0
ECHO File/Dir= %1
ECHO.
rem Path replaced by SVE based on where SVE was ran from
set ffmpegPath="<?<ffmpegPath>?>"
rem Check a few places where ffmpeg might be just in case SVE is moved, or the file is shared to others
ECHO #Checking for ffmpeg.exe
if exist "%~dp0..\ffmpeg.exe" (
    ECHO Detected ffmpeg in script parent folder
    set ffmpegPath="%~dp0..\ffmpeg.exe"
)
rem Path for SVE users using program files directory
if exist "C:\Program Files\Simple Video Editor\ffmpeg.exe" (
    ECHO Detected ffmpeg in SVE installation
    set ffmpegPath="C:\Program Files\Simple Video Editor\ffmpeg.exe"
)
rem Path for people who put the exe in the same folder as the scripts
if exist "ffmpeg.exe" (
    ECHO Detected ffmpeg in working directory
    set ffmpegPath="ffmpeg.exe"
)
ECHO.

rem Warn inexperienced users who double click the file about how to use it correctly
if [%1] == [] goto missingInput
goto hasInput

:missingInput
ECHO No source file/directory was provided
ECHO Please drag and drop a source video or directory containing source videos onto the .bat file, or provide it as a command line argument
rem Exit script and set error level
pause
exit /b 2

:hasInput
rem Check if the input is a directory or file
set Attribute=%~a1
set DirectoryAttribute=%Attribute:~0,1%
if /I "%DirectoryAttribute%"=="d" goto isDirectory
goto isFile

:isDirectory
ECHO Creating output directory %~1\SHINY
mkdir %~1\SHINY
ECHO.
rem Loop over each file in the directory, applying the SVE produced script to each file
rem Contents should have input replaced with %%F by SVE, and output replaced with a new directory like "%~dp1\SHINY\%%~nF.ext"
ECHO #Applying script to all files in "%~1"
SETLOCAL EnableDelayedExpansion
for /f "delims=" %%F in ('dir /b /a-d %1\*<?<SVESourceExt>?> ^| sort') do (
    ECHO %%~F
    call %ffmpegPath% -v error -stats <?<SVE%%FContents>?>
)
ENDLOCAL
ECHO.
goto endProcess

:isFile
ECHO Creating output directory %~dp1\SHINY
mkdir %~dp1\SHINY
ECHO.
rem Apply SVE produced script to a single file
rem Input should be replaced with "%~1" by SVE
ECHO #Applying script to "%~1"
call %ffmpegPath% -v error -stats <?<SVEContents>?>
ECHO.

:endProcess
ECHO Process finished, you may close this window.
pause