@ECHO off
ECHO This batch script was generated by Simple Video Editor <?<SVEVersion>?>
ECHO #Batch inputs
ECHO WorkingDir= %cd%
ECHO BatchDir= %~dp0
ECHO File/Dir= %1
ECHO.
rem Set name of subdirectory to create and use for outputs generated by the batch script
set outDirectoryName=SHINY

rem Path replaced by SVE based on where SVE was ran from
set ffmpegPath="<?<ffmpegPath>?>"
rem Check a few places where ffmpeg might be just in case SVE is moved, or the file is shared to others
ECHO #Checking for ffmpeg.exe
if exist "%~dp0..\ffmpeg.exe" (
    ECHO Detected ffmpeg in script parent folder
    set ffmpegPath="%~dp0..\ffmpeg.exe"
)
rem Path for SVE users using program files directory
if exist "C:\Program Files\Simple Video Editor\ffmpeg.exe" (
    ECHO Detected ffmpeg in SVE installation
    set ffmpegPath="C:\Program Files\Simple Video Editor\ffmpeg.exe"
)
rem Path for people who put the exe in the same folder as the scripts
if exist "ffmpeg.exe" (
    ECHO Detected ffmpeg in working directory
    set ffmpegPath="ffmpeg.exe"
)
ECHO.

rem Warn inexperienced users who double click the file about how to use it correctly
if [%1] == [] goto missingInput
goto hasInput

:missingInput
ECHO No source file/directory was provided
ECHO Please drag and drop source video(s) or a directory containing source videos onto the .bat file, or provide it as a command line argument
pause
rem Exit script and set error level
exit /b 2

:hasInput
rem Check if the input is a directory or file
set Attribute=%~a1
set DirectoryAttribute=%Attribute:~0,1%
if /I "%DirectoryAttribute%"=="d" goto isDirectory
goto isFile

:isDirectory
ECHO Creating output directory %~1\%outDirectoryName%
mkdir "%~1\%outDirectoryName%"
ECHO.
rem Loop over each file in the directory, applying the SVE produced script to each file
rem Contents should have input replaced with %%F by SVE, and output replaced with a new directory like "%~dp1\%outDirectoryName%\%%~nF.ext"
ECHO #Applying script to all files in "%~1"
SETLOCAL EnableDelayedExpansion
for /f "delims=" %%F in ('dir /b /a-d %~1\*<?<SVESourceExt>?> ^| sort') do (
    ECHO %%~F
    call %ffmpegPath% -v error -stats <?<SVEDirLoopContents>?>
)
ENDLOCAL
ECHO.
goto endProcess

:isFile
ECHO Creating output directory %~dp1\%outDirectoryName%
mkdir "%~dp1\%outDirectoryName%"
ECHO.
rem Apply SVE produced script to each file argument one by one
rem Contents should have input replaced with "%%~F" by SVE, and output like "%%~dpF\%outDirectoryName%\%%~nF.ext"
SETLOCAL EnableDelayedExpansion
ECHO #Applying script to all arguments
for %%F in (%*) do (
    ECHO %%~F
    call %ffmpegPath% -v error -stats <?<SVEArgLoopContents>?>
)
ENDLOCAL
ECHO.

:endProcess
ECHO Process finished, you may close this window.
pause